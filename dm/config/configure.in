##START#########################################################################
#
# $URL$
# $Revision$
# $Author$
#
# Description: Autoconf input file for LM.
#
# $McHugh_Copyright-Start$
#
# Copyright (c) 2000-2007
# McHugh Software International
# Waukesha, Wisconsin
#
# This software is furnished under a corporate license for use on a
# single computer system and can be copied (with inclusion of the
# above copyright) only for use on such a system.
#
# The information in this document is subject to change without notice
# and should not be construed as a commitment by McHugh Software
# International.
#
# McHugh Software International assumes no responsibility for the use of
# the software described in this document on equipment which has not been
# supplied or approved by McHugh Software International.
#
# $McHugh_Copyright-End$
#
##END###########################################################################

dnl ----------------------------------------------------------------------------
dnl Define the AC_TAGGED_VERSION function.
dnl ----------------------------------------------------------------------------

AC_DEFUN(AC_TAGGED_VERSION,
[
    if test ! -r TaggedVersion; then
        AC_MSG_ERROR(You must have a TaggedVersion file.)
    fi
    TAGGED_VERSION=`cat TaggedVersion`;
])

dnl ----------------------------------------------------------------------------
dnl Define the AC_PATH_PERL5 function.
dnl ----------------------------------------------------------------------------

AC_DEFUN(AC_PATH_PERL5,
[# Extract the first word of "$2", so it can be a program name with args.
AC_MSG_CHECKING([for Perl 5 or newer])
AC_CACHE_VAL(ac_cv_path_$1,
[case "[$]$1" in
  /*)
  ac_cv_path_$1="[$]$1" # Let the user override the test with a path.
  ;;
  *)
  IFS="${IFS=   }"; ac_save_ifs="$IFS"; IFS="${IFS}:"
  for ac_dir in $PATH; do
    test -z "$ac_dir" && ac_dir=.
    if test -f $ac_dir/perl; then
      changequote(<<, >>)dnl
      ac_cv_version_$1=`$ac_dir/perl -v | grep -i 'version' | sed -e 's/[^0-9.]*//'`
      changequote([, ])dnl
      case "[$]ac_cv_version_$1" in
      1*|2*|3*|4*)
        continue
        ;;
      *)
        ac_cv_path_$1="$ac_dir/perl"
        break
        ;;
      esac
    fi
  done
  IFS="$ac_save_ifs"
  ;;
esac])dnl
$1="$ac_cv_path_$1"
if test -n "[$]$1"; then
  AC_MSG_RESULT([$]$1)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR(You must have Perl 5.000 or newer installed and in your path.)
fi
AC_SUBST($1)dnl
])

dnl ----------------------------------------------------------------------------
dnl Define the AC_REQ_ENVVAR function.
dnl ----------------------------------------------------------------------------

AC_DEFUN(AC_REQ_ENVVAR, [
if eval test \"\$$1\" = \"\"; then
  AC_MSG_ERROR($1 is not exported.)
fi
])

dnl ----------------------------------------------------------------------------
dnl Do everything involved in initializing ourselves.
dnl ----------------------------------------------------------------------------

AC_INIT(../src/libsrc/lmint)

AC_CONFIG_AUX_DIR(./)

AC_REQ_ENVVAR(MOCADIR)
AC_REQ_ENVVAR(MCSDIR)
AC_REQ_ENVVAR(SALDIR)
AC_REQ_ENVVAR(SLDIR)

dnl ----------------------------------------------------------------------------
dnl Get the tagged version and set the release version.
dnl ----------------------------------------------------------------------------

AC_MSG_CHECKING(release)

AC_ARG_WITH(RELEASE,
    --with-RELEASE=[Y|N],
    RELEASE="$withval")

if test "$RELEASE" != "Y"; then
    RELEASE_VERSION="\"Build Date: \" __DATE__"
    DB_RELEASE_VERSION="dev"
else
    AC_TAGGED_VERSION
    RELEASE_VERSION="\"$TAGGED_VERSION\""
    DB_RELEASE_VERSION="$TAGGED_VERSION"
fi

AC_MSG_RESULT($DB_RELEASE_VERSION)

AC_SUBST(RELEASE_VERSION)
AC_SUBST(DB_RELEASE_VERSION)

dnl ----------------------------------------------------------------------------
dnl Get the platform we're running on.
dnl ----------------------------------------------------------------------------

AC_CANONICAL_HOST

dnl ----------------------------------------------------------------------------
dnl Set the platform name as LM defines it.
dnl ----------------------------------------------------------------------------


case "$host_os" in
aix*)
    platform="aix-powerpc"
    shlibext="so"
    ;;
linux*)
    platform="linux-x64"
    shlibext="so"
    ;;
solaris*)
    platform="solaris-sparc"
    shlibext="so"
    ;;
*)
    AC_MSG_ERROR(You are on an unsupported platform.)
    ;;
esac

dnl ----------------------------------------------------------------------------
dnl Set the top level directory.
dnl ----------------------------------------------------------------------------

AC_MSG_CHECKING(LM directory)
LMDIR=`dirname \`pwd\``
AC_SUBST(LMDIR)
AC_MSG_RESULT($LMDIR)

dnl ----------------------------------------------------------------------------
dnl Checks for the pathnames to programs.
dnl ----------------------------------------------------------------------------

AC_PATH_PERL5(PERL_PATH)
AC_PATH_PROG(TR_PATH,    tr,    tr)
AC_PATH_PROG(UNAME_PATH, uname, uname)

dnl ----------------------------------------------------------------------------
dnl Build the output file argument list.  This involves just looking for files
dnl that end with a '.in' extension and adding it to the AC_OUTPUT arg list.
dnl ----------------------------------------------------------------------------

my_ac_output_arg="../.profile:profile.in"
my_ac_output_arg="$my_ac_output_arg ../makefiles/Config.mk:Config.mk.$platform"

for my_ac_input_file in `find ../db/data/load -type f -name "*.csv.in"`
do
    my_ac_output_file=`echo $my_ac_input_file | sed -e "s/\.in$//g"`
    my_ac_output_arg="$my_ac_output_arg $my_ac_output_file:$my_ac_input_file"
done

for my_ac_input_file in `find ../src/incsrc -type f -name "*.h.in"`
do
    my_ac_output_file=`echo $my_ac_input_file | sed -e "s/\.in$//g"`
    my_ac_output_arg="$my_ac_output_arg $my_ac_output_file:$my_ac_input_file"
done

for my_ac_input_file in `find ../scripts -type f -name "*.pl.in"`
do
    my_ac_output_file=`echo $my_ac_input_file | sed -e "s/\.in$//g"`
    my_ac_output_arg="$my_ac_output_arg $my_ac_output_file:$my_ac_input_file"
done

dnl ----------------------------------------------------------------------------
dnl Set Directory For GCC libraries
dnl ----------------------------------------------------------------------------
my_gcc_path=`gcc -print-search-dirs|grep install|cut -d ' ' -f 2`
#Dynamically setting GCCLIB for REDHAT LINUX. 
#Example GCCLIB should look something like /usr/lib/gcc/x86_64-redhat-linux/4.4.7/ /usr/lib/gcc/x86_64-redhat-linux/4.4.7/crtbeginS.o /usr/lib/gcc/x86_64-redhat-linux/4.4.7/crtendS.o /usr/lib64/libstdc++.so.6
if test -d $my_gcc_path; then
  GCCLIB="$my_gcc_path $my_gcc_path""crtbeginS.o $my_gcc_path""crtendS.o /usr/lib64/libstdc++.so.6"
else
  GCCLIB=""
fi
AC_SUBST(GCCLIB)

dnl ----------------------------------------------------------------------------
dnl Generate output files.
dnl ----------------------------------------------------------------------------

AC_OUTPUT($my_ac_output_arg)

dnl ----------------------------------------------------------------------------
dnl Make Perl scripts executable.
dnl ----------------------------------------------------------------------------

echo "making scripts executable"

for my_input_file in `find ../scripts -type f -name "*.pl.in"`
do
    my_script=`echo $my_input_file | sed -e "s/\.in$//g"`
    chmod 555 $my_script
done
